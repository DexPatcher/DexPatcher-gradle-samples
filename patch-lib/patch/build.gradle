// DexPatcher Patched Application plugin:

apply plugin: 'com.github.lanchon.dexpatcher.patch-library'

// The DexPatcher Patch Library plugin produces a DexPatcher patch library that bundles
// compiled changes to code, manifest and resources of a source application without
// bundling any part of the application itself. The changes bundled in a patch library
// are applied to its source application using the DexPatcher Patched Application plugin.

dependencies {
    // The source application is defined as a Gradle dependency, which can contain
    // either an Android APK or a DexPatcher APK library.

    // In this case we use the APK library produced by the ':apklib' subproject:
    sourceApkLibrary project(':apklib')

    // Please note that in Android Studio you may need to invoke 'Build/Clean Project'
    // to solve Gradle sync issues when using project dependencies.
}

dexpatcherConfig {
    patchLibrary {
        // Displays package and version information of the source app on every build.
        // Note that this info can also be queried by invoking the 'sourceAppInfo' task.
        // Defaults to false.
        //printAppInfo = false;

        // Enable import of Java symbols from the source app. Defaults to true.
        //importSymbols = true;

        // Disable detection of invalid resources and attempt to process them normally.
        // Defaults to false.
        //disableResourceValidation = true

        // Build the patched app using a custom AAPT2 binary bundled with the version of
        // Apktool currently in use. These AAPT2 binaries are modified to allow rebuilding
        // of slightly out-of-spec apps, such as apps that contain invalid resources.
        // Defaults to false.
        //useAapt2BundledWithApktool = true

        //dexpatcherAnnotationClasspath.setFrom project.files(...)
    }

    dex2jar {
    }
}

android {
    compileSdkVersion 21
    //buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName '1.0'
    }

    lintOptions {
        disableLintTasks true
        //checkReleaseBuilds false
    }
}

dependencies {
    implementation('ru.bartwell:exfilepicker:1.8') {
        exclude module: 'support-v4'
    }
}

void disableLintTasks(boolean disableTasks) {
    if (disableTasks) {
        tasks.withType(com.android.build.gradle.tasks.LintBaseTask).configureEach {
            enabled = false
        }
    }
}
