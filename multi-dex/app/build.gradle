apply plugin: 'com.github.lanchon.dexpatcher.patched-application'

dexpatcherConfig {
    patchedApplication {
        printAppInfo = true;

        // Speed up debug builds by enabling multi-threaded multi-dex processing for them,
        // but only if multi-dex mode is already enabled for them. Defaults to true.
        //multiDexThreadedForMultiDexDebugBuilds = true

        // Speed up debug builds by enabling multi-threaded multi-dex processing for them,
        // even if multi-dex mode is not already enabled for them. Note that apps patched
        // in multi-dex mode are only compatible with devices running Android 5.0 (API 21)
        // and later. Defaults to false.
        //multiDexThreadedForAllDebugBuilds = true
    }

    dexpatcher {
        // MULTI-DEX TEST

        // In this test we patch a multi-dex source app created by artificially limiting
        // the maximum number of method references per dex file. It was created using
        // DexPatcher tool's '--max-dex-pool-size' option.

        // DexPatcher multi-dex patching mode is automatically enabled if multi-dex is
        // enabled in the Android build system. However, you can also enable multi-dex
        // patching manually:
        //multiDex = true
        // Note that multi-dex need not be enabled in the Android build system unless the
        // patch dex itself is large and requires it. The dex sizes of the source app and
        // the patched app are not relevant.

        // In this test we also limit the number of methods per dex file in the patched app:
        maxDexPoolSize = 10000
        // This forces it to become multi-dex for testing purposes.

        // And finally we enable very verbose logging:
        verbosity = DEBUG
        logStats = true
        // This will show multi-dex I/O in action.
    }
}

android {
    compileSdkVersion 21
    //buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId 'makeinfo.com.getid'

        // Enable multi-dex support in the Android build system and DexPatcher:
        multiDexEnabled true

        // DexPatcher supports native multi-dex but does not support legacy multi-dex.
        // Patched multi-dex apps require native multi-dex platform support, which was
        // implemented in ART and included in Android 5.0 (API 21) and later versions.

        // The 'minSdkVersion' should be set to 21 or higher in patched multi-dex apps:
        //minSdkVersion 9
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName '1.0'
    }

    lintOptions {
        disableLintTasks true
        //checkReleaseBuilds false
    }
}

dependencies {
    implementation('ru.bartwell:exfilepicker:1.8') {
        exclude module: 'support-v4'
    }
}

void disableLintTasks(boolean disableTasks) {
    if (disableTasks) {
        tasks.withType(com.android.build.gradle.tasks.LintBaseTask).configureEach {
            enabled = false
        }
    }
}
